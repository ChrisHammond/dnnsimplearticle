
IF EXISTS (SELECT * FROM dbo.sysobjects WHERE id = object_id(N'{databaseOwner}[{objectQualifier}DNNSimpleArticle_spGetArticles]') and OBJECTPROPERTY(id, N'IsProcedure') = 1)
	DROP PROCEDURE {databaseOwner}{objectQualifier}DNNSimpleArticle_spGetArticles
GO

CREATE PROCEDURE {databaseOwner}[{objectQualifier}DNNSimpleArticle_spGetArticles]
   @ModuleId INT,
   @PageSize INT,
   @PageIndex INT
AS 
   BEGIN
       DECLARE @RowStart INT 
       DECLARE @RowEnd INT 
               
       SET @RowStart = @PageSize * @PageIndex + 1 ; 
       SET @RowEnd = @RowStart + @PageSize - 1 ; 
       
       SELECT  *
       FROM    ( SELECT    
			a.Id,
			a.ModuleId,
			a.Title,
			a.Description,
			a.Body,
			a.CreatedOnDate,
			a.CreatedByUserId,
			a.LastModifiedOnDate,
			a.LastModifiedByUserId,
			( SELECT    TotalRecords = COUNT(*)
                        	FROM
					{databaseOwner}{objectQualifier}DNNSimpleArticle_Article a
					INNER JOIN {databaseOwner}{objectQualifier}ContentItems AS CI ON a.ContentItemId = CI.ContentItemID
				WHERE 
					a.ModuleId = @ModuleId
                        ) AS TotalRecords,
			CI.ContentItemID,
			CI.Content,
			CI.ContentTypeID,
			CI.TabiD,
			CI.ContentKey,
			CI.Indexed,
			ROW_NUMBER() OVER ( ORDER BY a.CreatedOnDate DESC) AS RowNumber
		FROM
			{databaseOwner}{objectQualifier}DNNSimpleArticle_Article a
			INNER JOIN {databaseOwner}{objectQualifier}ContentItems AS CI ON a.ContentItemId = CI.ContentItemID
		WHERE 
			a.ModuleId = @ModuleId

               ) AS ArticleInfo
       WHERE   RowNumber >= @RowStart
               AND RowNumber <= @RowEnd 
   END
GO




IF EXISTS (SELECT * FROM dbo.sysobjects WHERE id = object_id(N'{databaseOwner}[{objectQualifier}DNNSimpleArticle_spGetArticle]') and OBJECTPROPERTY(id, N'IsProcedure') = 1)
	DROP PROCEDURE {databaseOwner}{objectQualifier}DNNSimpleArticle_spGetArticle
GO

CREATE PROCEDURE {databaseOwner}{objectQualifier}DNNSimpleArticle_spGetArticle
	@ArticleId int
AS
SELECT 
	a.[Id],
	a.ModuleId,
	a.Title,
	a.Description,
	a.Body,
	a.CreatedOnDate,
	a.CreatedByUserId,
	a.LastModifiedOnDate,
	a.LastModifiedByUserId,
	( SELECT    TotalRecords = COUNT(*)
                	FROM
			{databaseOwner}{objectQualifier}DNNSimpleArticle_Article a
			INNER JOIN {databaseOwner}{objectQualifier}ContentItems AS CI ON a.ContentItemId = CI.ContentItemID
		WHERE 
			a.id = @ArticleId
                ) AS TotalRecords,

	CI.ContentItemID,
	CI.Content,
	CI.ContentTypeID,
	CI.TabiD,
	CI.ContentKey,
	CI.Indexed
FROM
	{databaseOwner}{objectQualifier}DNNSimpleArticle_Article a
	INNER JOIN {databaseOwner}{objectQualifier}ContentItems AS CI ON a.ContentItemId = CI.ContentItemID
WHERE 
	ID = @ArticleId
GO

